// Persistence of Vision Ray Tracer Scene Description File
// Persistence of Vision Ray Tracer Scene Description File
// File: Honeycomb.pov

#version 3.6; // current version is 3.8

/* 
Information on Pov-Ray:
 
My personal introduction into Pov-Ray was the excellent book "3D-Welten, professionelle Animationen und fotorealistische Grafiken mit Raytracing" from 
Toni Lama by Carl Hanser Verlag MÃ¼nchen Wien, 2004. Apart of that I recommend the Pov-Ray-homepage (http://www.povray.org).

Further information on Pov-Ray can be found at https://sus.ziti.uni-heidelberg.de/Lehre/WS2021_Tools/POVRAY/POVRAY_PeterFischer.pdf,  
https://wiki.povray.org/content/Main_Page, https://de.wikibooks.org/wiki/Raytracing_mit_POV-Ray or, in german language, here: https://www.f-lohmueller.de/pov_tut/pov__ger.htm
*/ 
 
/*
---------------------------------------------------Modeling approach---------------------------------------------- 

This file is made to demonstrate my approach for gemerating cellular structures in Pov-Ray. As a simple model a honeycomb is used. 
Each cell of this honeycomb is produced from a blob where 6 peripheral spheres are substracted from one central sphere. 
Cells are then substracted from a larger mass to produce the honeycomb. 
*/ 
                

//-----------------------------------Scene settings (Camera, light, background)-------------------------------------------------

global_settings {
    assumed_gamma 1.0
    max_trace_level 5
}


#declare Cameraz = camera {
    location  <0, 1, 10>
    right     x*image_width/image_height
    look_at   <0, 0,  0>
}


camera {
    Cameraz
}



// create a regular point light source
light_source {
    0*x                  // light's position (translated below)
    color rgb <1,1,1>    // light's color
    translate <10, 10, 100>
}  




//---------------------------Objects-----------------------------------------------------------------

difference {  
    union{                                                         //A set of 8*8 blobs is generated
        #declare tickery = -8;
        #while (tickery < 8)
            #declare tickerx = -8;
            #while (tickerx < 8)
                blob {
                    threshold 0.7
                    sphere { 
                        < 0,   0,    0>, 1, 1 
                    }  
                    #declare ticker = 0;                            //Honeycomb cells are generated by substracting spheres from all 6 sides in the individual blobs
                    #while (ticker < 6)
                        sphere { 
                            <-0.6,  0, 0>, 0.425, -1
                            scale <1,1,1.4> 
                            rotate <0, 0, 60 * ticker>
                        } 
                    #declare ticker = ticker + 1;
                    #end
                translate <0.6*tickerx + 0.3 * mod(tickery,2), 0.6 * cos(radians(30))*tickery,  0>
                }
            #declare tickerx = tickerx + 1;
            #end
        #declare tickery = tickery + 1;
        #end
        pigment {
            color rgb <1,0.8,0.>     // solid color pigment
        }
    }
      box { 
        <-6, -5, 0>, <6, 5, 2>                                      //Part of the cells is removed for better viewing
        pigment {
            color rgb <1,1,0.5>     // solid color pigment
        }
    }  

} 

